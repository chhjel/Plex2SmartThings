/**
 *  Plex Plus Child
 *
 *  Copyright 2015 Christian Hjelseth / Jake Tebbett
 *
 *  Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
 *  in compliance with the License. You may obtain a copy of the License at:
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software distributed under the License is distributed
 *  on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License
 *  for the specific language governing permissions and limitations under the License.
 * 
 * VERSION CONTROL - Plex Plus Child
 * ###############
 *
 *  v2.0 - Modified Christian Hjelseth's excellent Smart App to use parent & child apps
 * 				- Allowing multi room matching to a single player
 * 				- Allowing flexible number of "rooms"
 * 		 - Added support for matching against Player Name or User Name or IP Address (IP address requires Jake's modified exe)
 *		 - Added support for on and off switches on the same state change.
 *		 - Added disabled switch to stop automation
 *	v2.1 - Removed independant debug logging from child app, now mastered from parent.
 *		 - Fix bug with disable switch
 *		 - Added Hue Colors (First select Hues)
 *  v2.2 - Changes to order of items in App
 *		 - Added momentary switch, better for triggering scenes in other apps (doesn't trigger a second time on toggle off)
 *  v2.3 - Cosmetic changes including hiding dimmer settings when no dimmers selected.
 *  v2.4 - Added the ability to return switches to the state before Plex started playing.
 *  v2.5 - Updated icon path
 *  v2.6 - 02/05/16 - Added MusicPlayer device handler support for Plex HT SmartApp support, fixed some bugs, made code more efficient.
 *  v2.7 - 05/05/16 - Further code efficiencies and also added support for trailers with Plex media device type.
 *  v2.8 - 02/07/16 - Removed routine handling, due to errors on some android devices. Added running only in selected modes.
 *  v2.9 - 04/07/16 - Added cancellable delay on stop action.
 *  v2.10- 07/07/16 - Re-worded bulbs section
 *  v2.11- 24/09/16 - Added support for delay on pause, triggering based on type of media and fix to debugging
 *  v2.11- 16/10/16 - Bug fix on pause delay (was using stop delay)
 *  v2.12- 21/10/16 - Code optimisation, better app layout and support for pushing to a Plex Plus or Plex HT Device Type
 */



definition(
    name: "Plex Plus Child",
    namespace: "jebbett",
    author: "Christian Hjelseth & Jacob Tebbett",
    description: "Allows web requests to dim/turn off/on lights when plex is playing",
    category: "My Apps",
    parent: "jebbett:Plex Plus",
    iconUrl: "https://raw.githubusercontent.com/jebbett/Plex2SmartThings/PlexPlus/icon.png",
    iconX2Url: "https://raw.githubusercontent.com/jebbett/Plex2SmartThings/PlexPlus/icon.png",
    iconX3Url: "https://raw.githubusercontent.com/jebbett/Plex2SmartThings/PlexPlus/icon.png")


preferences {
    page name: "mainPage", install: false, uninstall: true
    page name: "pageDoThis"
    page name: "pageWhenThis"
    page name: "pageMediaSettings"

    
}

def installed() {
    log.debug "Installed with settings: ${settings}"
    initialize()
}

def updated() {
    log.debug "Updated with settings: ${settings}"
    unschedule()
    initialize()
}

def initialize() {
	unsubscribe()
	state.catcherRunning = false
    subscribe(playerDT, "status", PlayerDTCommandRecieved)
}

// Main page to select room settings
def mainPage() {
        
    dynamicPage(name: "mainPage", uninstall: true, install: true) {
        
        section() {
                label title: "Enter Room Name", defaultValue: app.label, required: false
        }
        section ("When this happens"){
        	href(name: "pageWhenThis", title:"When Event Comes From", description: "", page: "pageWhenThis", required: true, state: "complete", image: "https://cdn0.iconfinder.com/data/icons/round-ui-icons/128/tick_blue.png")      

        	href(name: "pageDoThis", title:"Trigger these actions", description: "", page: "pageDoThis", required: true, state: "complete", image: "https://cdn0.iconfinder.com/data/icons/round-ui-icons/128/favourite_blue.png")      

        	href(name: "pageMediaSettings", title:"With these settings", description: "", page: "pageMediaSettings", required: true, state: "complete", image: "https://cdn0.iconfinder.com/data/icons/round-ui-icons/128/setting_blue.png")      
      	}
    }
}


def pageDoThis(){
	dynamicPage(name: "pageDoThis", uninstall: false) {
        section("Lights") {
			input "dimmers1", "capability.switchLevel", title: "Adjust level of these bulbs", multiple: true, required: false, submitOnChange: true
            input "hues1", "capability.colorControl", title: "Adjust level and color of these bulbs", multiple:true, required:false, submitOnChange: true
            if(hues1||dimmers1) {
            input(name: "iLevelOnPlay1", type: "number", title: "Level on Play", defaultValue:0)
            input(name: "iLevelOnPause1", type: "number", title: "Level on Pause", defaultValue:30)
            input(name: "iLevelOnStop1", type: "number", title: "Level on Stop", defaultValue:100)
            }
            if(hues1) {
				input "colorOnPlay", "enum", title: "Hue Bulbs > Color On Play", required: false, multiple: false, submitOnChange: true,
					options: ["Soft White", "White", "Daylight", "Warm White", "Red", "Green", "Blue", "Yellow", "Orange", "Purple", "Pink"]
                input "colorOnPause", "enum", title: "Hue Bulbs > Color On Pause", required: false, multiple: false, submitOnChange: true,
					options: ["Soft White", "White", "Daylight", "Warm White", "Red", "Green", "Blue", "Yellow", "Orange", "Purple", "Pink"]
                input "colorOnStop", "enum", title: "Hue Bulbs > Color On Stop", required: false, multiple: false, submitOnChange: true,
					options: ["Soft White", "White", "Daylight", "Warm White", "Red", "Green", "Blue", "Yellow", "Orange", "Purple", "Pink"]
            }
            input(name: "bDimOnlyIfOn1", type: "bool", title: "Dim bulbs only if they're already on")
        }
		section("Switches") {
        	input "switches2", "capability.switch", title:"Switches On when Playing", multiple: true, required: false
            input "switches1", "capability.switch", title:"Switches Off when Playing", multiple: true, required: false
            input(name: "bReturnState1", type: "bool", title: "Switches return to original state when Stopped")
            input(name: "bSwitchOffOnPause1", type: "bool", title: "Switches use Play config when Paused")
            paragraph "The below switches do not toggle off when state becomes inactive, ideal for tiggering external App scenes"
            input "mSwitchPlay", "capability.switch", title:"Momentary switch on Play", multiple: true, required: false
            input "mSwitchPause", "capability.switch", title:"Momentary switch on Pause", multiple: true, required: false
            input "mSwitchStop", "capability.switch", title:"Momentary switch on Stop", multiple: true, required: false
            
        }
		section("Modes") {
			input "playMode1", "mode", title: "Mode when playing", required:false
			input "pauseMode1", "mode", title: "Mode when paused", required:false
			input "stopMode1", "mode", title: "Mode when stopped", required:false
		}
        section("Update This Device"){
        	input(name: "PlexPlusDT", type: "capability.musicPlayer", title: "Update This Device", description: "Use a Plex Plus or HT Custom Device", multiple: false, state: null, required:false)
        }
	}
}


def pageWhenThis(){
	dynamicPage(name: "pageWhenThis", uninstall: false) {
    	section("When Plex2SmartThings sends and event matching:") {
            input(name: "playerA1", type: "text", title: "Player name, User or IP", required:false)
            input(name: "playerB1", type: "text", title: "Player name, User or IP (alternative)", required:false)
        }
        section("Or when a media player device changes state:") {
            input(name: "playerDT", type: "capability.musicPlayer", title: "ST Media Player Device", multiple: false, required:false)
        }
        section("Notes"){
        	paragraph "To identify player, you can use either Player Device Name, Username, IP address or * in order to match any player where using the Plex2SmartThings program on your computer. \n\nOr you can use a supported media player device type."
       }
	}
}

def pageMediaSettings(){
	dynamicPage(name: "pageMediaSettings", uninstall: false) {
    	section("Media Settings") {	
			input(name: "bTreatTrailersAsPause1", type: "bool", title: "Use pause config for movie trailers")
            input(name: "stopDelay", type: "number", title: "Delay stop action", required:true, defaultValue:5)
            input(name: "pauseDelay", type: "number", title: "Delay pause action", required:true, defaultValue:3)
		}
        section("Restrictions") {
			input "mediaTypeOk", "enum", title: "Only for media types:", multiple: true, submitOnChange: true, required: false,
			options: ['movie', 'episode', 'clip', 'track']
        	input "disabled", "capability.switch", title: "Switch to disable when On", required: false, multiple: false
            input "activeMode", "mode", title: "Only run in selected modes", multiple: true, required:false
        }
	}
}


// Recieve command from MusicPlayer device type

def PlayerDTCommandRecieved(evt){
	
	//If no device type configured do not run the below code
	if(!playerDT){return}
    
	if(evt.value=="playing"){AppCommandRecieved("onplay", "Unknown", playerDT,"ST Media Player Device", playerDT.currentplaybackType)}
	else if(evt.value=="stopped"){AppCommandRecieved("onstop", "Unknown", playerDT,"ST Media Player Device", playerDT.currentplaybackType)}
    else if(evt.value=="paused"){AppCommandRecieved("onpause", "Unknown", playerDT,"ST Media Player Device", playerDT.currentplaybackType)}
    
}


def AppCommandRecieved(command, userName, playerName, playerIP, mediaType) {

//Log last event
	parent.StoreLastEvent(command, userName, playerName, playerIP, mediaType)

//Check if room found
	def allowedDevs = ["*", "$playerIP", "$playerName", "$userName"]
    
    if (allowedDevs.contains("${playerA1}")){logWriterChild ("->Player 1 Match: ${app.label}")}
    else if (allowedDevs.contains("${playerB1}")){logWriterChild ("->Player 2 Match: ${app.label}")}
    else if ("$playerIP" == "ST Media Player Device"){logWriterChild ("->ST Device Type Match: ${app.label}")}
    else{logWriterChild ("->No match found for room: ${app.label}"); return}
    
// Stop running if disable switch is activated    
    if (disabled != null) {if(disabled.currentSwitch == "on") {logWriterChild ("->Disabled via switch"); return}}
    if (activeMode != null && !activeMode.contains(location.mode)) {logWriterChild ("->Disabled via invalid mode"); return}

// Check if Media Type is correct
	if(mediaTypeOk){
		def mediaTypeFound = mediaTypeOk.find { item -> item == mediaType}
    	if(mediaTypeFound == null) {logWriterChild ("->Match NOT found for media type: ${mediaType}"); return}
	}
    
//Translate play to pause if bTreatTrailersAsPause is enabled for this room
    if(bTreatTrailersAsPause1 && mediaType == "clip" && command == "onplay") {command = "onpause"}

// Unschedule delays
	unschedule(StopCommand)
    unschedule(PauseCommand)

// Send media type to Plex Plus Device Type if configured.
	try { settings.PlexPlusDT?.playbackType("${mediaType}") }
	catch (Exception e) {log.info "Playback Type Not Supported: $e"}

// Play, Pause or Stop
    if (command == "onplay") {
    	logWriterChild ("->Playing")
        PlayCommand()
    }
    else if (command == "onpause") {        
        logWriterChild ("->Paused")
        if(settings.pauseDelay){
        	if(pauseDelay == "0"){PauseCommand()}else{
            	logWriterChild ("-> Pause Action Delay")
        		runIn(pauseDelay, PauseCommand)
        	}
    	}
    }
    else if (command == "onstop") {
        logWriterChild ("->Stopped")
        if(settings.stopDelay){
        	if(stopDelay == "0"){StopCommand()}else{
            	logWriterChild ("-> Stop Action Delay")
        		runIn(stopDelay, StopCommand)
        	}
        }
    }
}

def PlayCommand(){
	if(!state.catcherRunning){
        catchState("switches1")
    	catchState("switches2")
        state.catcherRunning = true
    }
    if(settings?.playMode1){setLocationMode(playMode1)}
	SetLevels(iLevelOnPlay1, colorOnPlay)
    SetSwitchesOff()
    mSwitchPlay?.on()
    PlexPlusDT?.play()
}

def PauseCommand(){
	
    if(settings?.pauseMode1){setLocationMode(pauseMode1)}
   	SetLevels(iLevelOnPause1, colorOnPause)
    mSwitchPause?.on()
    PlexPlusDT?.pause()
    if(bSwitchOffOnPause1) {
   		SetSwitchesOff()
    } else {
       	if(state.catcherRunning && bReturnState1){
       		returnToState("switches1")
   			returnToState("switches2")
           	state.catcherRunning = false
       	}else{
       		SetSwitchesOn()
           	state.catcherRunning = false
       	}
    }
}

//Stop command
def StopCommand(){

	if(settings?.stopMode1){setLocationMode(settings?.stopMode1)}
    SetLevels(iLevelOnStop1, colorOnStop)
    mSwitchStop?.on()
    if(state.catcherRunning && bReturnState1){
       	returnToState("switches1")
    	returnToState("switches2")
        state.catcherRunning = false
    }else{
       	SetSwitchesOn()
        state.catcherRunning = false
    }
    PlexPlusDT?.stop()
}


// Actions
def SetSwitchesOn() {
	switches1?.on()
    switches2?.off()
}
def SetSwitchesOff() {
	switches1?.off()
    switches2?.on()
}

def SetLevels(level, acolor) {
	// If color specified set hues
    if (level != null) {        
    	def hueColor = 23
		def saturation = 56
		switch(acolor) {
			case "White":
				hueColor = 52
				saturation = 19
				break;
			case "Daylight":
				hueColor = 53
				saturation = 91
				break;
			case "Soft White":
				hueColor = 23
				saturation = 56
				break;
			case "Warm White":
				hueColor = 20
				saturation = 80 //83
				break;
			case "Blue":
				hueColor = 70
				break;
			case "Green":
				hueColor = 35
				break;
			case "Yellow":
				hueColor = 25
				break;
			case "Orange":
				hueColor = 10
				break;
			case "Purple":
				hueColor = 75
				break;
			case "Pink":
				hueColor = 83
				break;
			case "Red":
				hueColor = 100
				break;
		}
        
        if (bDimOnlyIfOn1){
        	hues1?.each { hue -> if ("on" == hue.currentSwitch) {hue.setColor([hue: hueColor, saturation: saturation, level: level])}}
        	dimmers1?.each { bulb -> if ("on" == bulb.currentSwitch) {bulb.setLevel(level)}}
        }else{
        	hues1?.setColor([hue: hueColor, saturation: saturation, level: level])
            dimmers1?.setLevel(level)
        }
	}
}

//Save state
private catchState(switches) {
        settings."${switches}"?.each { switcher -> state."${switcher.id}State" = switcher.currentValue("switch")
        	logWriterChild (switcher.currentValue("switch"))
        }
}
//Return to state
private returnToState(switches) {
	settings."${switches}"?.each {switcher -> 
    	if(state."${switcher.id}State" == "on") {switcher.on()}
        if(state."${switcher.id}State" == "off") {switcher.off()}
    }
}

// Debug logging

private def logWriterChild(value) {	
    if(parent.debugLogging){log.debug "${value}"}
}